// START SYMBOL

wtf3
    : "Ich " verb1construct ", " opt_interjection conjunction " " person " " verb3construct "."
    ;

// PERSONS

person : __IRC_nick ;

// VERBS

verb1construct
    : verb1_simple
    | verb1_self
    | verb1_object_simple
    | verb1_object_movement
    | verb1_object_split
    | verb1_object_complex
    ;

verb1_simple
    :   ( "rede"
        | "stinke"
        )
        [ " " adverb ]
        [ " " adverbial ]
    ;

verb1_self
    :   ( "sehe"
        | "beschreibe"
        | "putze"
        | "schlage"
        | "langweile"
        | "aeussere"
        | "erschrecke"
        | "entspanne"
        )
        " mich"
        [ " " adverb ]
        [ " " adverbial ]
    ;

verb1_object_simple
    :   ( "beschreibe"
        | "putze"
        | "erschrecke"
        | "lese"
        | "esse"
        | "verunstalte"
        | "programmiere"
        )
        " "
        noun{art_m4, art_f4, art_n4}
        [ " " adverb ]
        [ " " adverbial ]
    |   ( "rede"
        )
        [ " " adverb ]
        [ " " adverbial ]
        " ueber "
        noun{art_m4, art_f4, art_n4}
    ;

verb1_object_movement
    :   ( "schlage"
        )
        " "
        noun{art_m4, art_f4, art_n4}
        [ " " adverb ]
        [ " " movement_adverbial ]
    ;

verb1_object_split
    :   ( "schaue"
        )
        " "
        noun{art_m4, art_f4, art_n4}
        [ " " adverb ]
        [ " " adverbial ]
        " an"
    ;

verb1_object_complex
    :   "verwechsle"
        [ " " adverb ]
        [ " " adverbial ]
        " "
        noun{art_m4, art_f4, art_n4}
        " mit "
        noun{art_m3, art_f3, art_n3}
    ;

verb3construct
    : verb3_self
    | verb3_object_simple
    | verb3_object_movement
    | verb3_object_complex
    ;

verb3_self
    :   "sich "
        [ adverb " " ]
        [ adverbial " " ]
        ( "sieht"
        | "beschreibt"
        | "putzt"
        | "schlaegt"
        | "langweilt"
        | "aeussert"
        | "erschreckt"
        | "entspannt"
        )
    ;

verb3_object_simple
    :   [ adverb " " ]
        [ adverbial " " ]
        noun{art_m4, art_f4, art_n4}
        " "
        ( "beschreibt"
        | "schlaegt"
        | "putzt"
        | "erschreckt"
        | "liest"
        | "isst"
        | "verunstaltet"
        | "anschaut"
        )
    ;

verb3_object_movement
    :   [ adverb " " ]
        noun{art_m4, art_f4, art_n4}
        [ movement_adverbial " " ]
        ( "schlaegt"
        )
    ;

verb3_object_complex
    :   [ adverb " " ]
        [ adverbial " " ]
        noun{art_m4, art_f4, art_n4}
        " mit "
        noun{art_m3, art_f3, art_n3}
        ( " verwechselt"
        )
    ;

// ARTICLES AND NOUNS

art_m1 : "der" | "ein" ;
art_m3 : "dem" | "einem" ;
art_m4 : "den" | "einen" ;

art_f1 : "die" | "eine" ;
art_f3 : "der" | "einer" ;
art_f4 : "die" | "eine" ;

art_n1 : "das" | "ein" ;
art_n3 : "dem" | "einem" ;
art_n4 : "das" | "ein" ;

noun{art_m, art_f, art_n}
    : art_m " ROFL-Copter"
    | art_n " Katzenzuechtungsbuch"
    | art_f " Schultasche"
    | ("Slashdot" | "YouPorn")
    | art_f " Zahnbuerste"
    | art_f " Klopapierrolle"
    | art_n " Pferd"
    | art_f " " ("Lehr" | "Auto" | "Flaschenpost") "-Industrie"
    | art_f " " ("Viagra" | "Cialis") "-Pille"
    | art_f " Cola-Dose"
    | art_f " Semmel"
    | art_n " Handy"
    | art_m " Saft"
    | art_f " Uhr"
    | art_n " Fenster"
    | art_f " " ("Kamera" | "Webcam")
    | (art_m | art_n) " Teller"
    | art_f " " ("Uni Wien" | "TU Wien")
    | art_n " Topic"
    | art_m " Clown"
    | art_m " Drucker"
    | art_m " Vogerlsalat"
    | art_f " Pizza-Porn-Seite"
    | art_m " " ("Handwerker" | "Installateuer" | "Programmierer")
    | art_n " " ("Buch" | "Bilderbuch" | "Linux-Buch")
    | art_f " Steckdose"
    | "Substi"
    | art_m " Substi-Ersatz"
    | art_n " Deo"
    | (art_f | art_n) " Cola"
    | art_n " Papier"
    | art_f " Praesentation"
    | art_f " Katze"
    | "Schmierfett"
    | art_m " Fleck"
    | art_f " Lesbe"
    | art_f " Teetasse"
    | art_m " Kugelschreiber"
    | art_m " Interpreter"
    ;

// ADVERBS AND ADVERBIALS

adverb
    :   ( "" | "" | "" | [ "vielleicht " | "etwas " ] )
        ( "kaeltlich" | "spielend" | "gespannt" | "gluecklich" | "verspannt"
        | "bloed" | "suspekt" | "begeistert" | "schauend" | "schnell" | "redend"
        | "zufrieden" | "gelangweilt" | "entspannt" | "begafft" | "ruhig"
        | "verkaufend"
        )
    ;

adverbial
    : ("ueber" | "auf" | "mit") " " noun{art_m3, art_f3, art_n3}
    | "zwischen " noun{art_m3, art_f3, art_n3} " und " noun{art_m3, art_f3, art_n3}
    ;

movement_adverbial
    : adverbial
    | ("durch" | "um" | "in") " " noun{art_m4, art_f4, art_n4}
    | ("aus" | "zu") " " noun{art_m3, art_f3, art_n3}
    ;

opt_interjection
    :   ""
    |   ""
    |   ""
    |   ( "foobar" | "oida" | "heast" | "uppsala" | "lalala" | "ramba zamba" )
        ", "
    ;

conjunction
    : "waehrend" | "zudem" | "bis" | "wodurch" | "da" | "solange" | "womit"
    ;
