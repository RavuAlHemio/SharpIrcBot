// START SYMBOL

wtf3
    : "Ich " verb1construct ", " opt_interjection conjunction " " chosen_person " " verb3construct "."
    ;

// PERSONS

person : __IRC_nick ;
chosen_person : __IRC_chosen_nick;

// VERBS

verb1construct
    : !!opt_B   <3> verb1construct_builtin
    | !opt_g    <1> verb1construct_wg
    ;

verb1construct_builtin
    : <6> verb1_simple
    | <1> verb1_simple_reqadverb
    | <1> verb1_simple_movement
    | <12> verb1_self
    | <1> verb1_self_movement
    | <17> verb1_object_simple
    | <1> verb1_object_movement
    | <1> verb1_object_split
    | <3> verb1_object_complex
    ;

verb1construct_wg
    :   wg_verb1
        [ " " adverb ]
        [ " " adverbial ]
    ;

verb1_simple
    :   ( "rede"
        | "stinke"
        | "spamme"
        | "sitze"
        | "liege"
        | "singe"
        )
        [ " " adverb ]
        [ " " adverbial ]
    ;

verb1_simple_reqadverb
    :   ( "klinge"
        )
        " " adverb
        [ " " adverbial ]
    ;

verb1_simple_movement
    :   ( "wichse"
        )
        [ " " adverb ]
        [ " " movement_adverbial ]
    ;

verb1_self
    :   ( "sehe"
        | "beschreibe"
        | "putze"
        | "schlage"
        | "langweile"
        | "erschrecke"
        | "entspanne"
        | "animiere"
        | "erschie\u00DFe"
        | "beschimpfe"
        | "fotografiere"
        | "verkaufe"
        )
        " mich"
        [ " " adverb ]
        [ " " adverbial ]
    ;

verb1_self_movement
    :   ( "entspanne"
        )
        " mich"
        [ " " adverb ]
        [ " " movement_adverbial ]
    ;

verb1_object_simple
    : <17>  ( "beschreibe"
            | "putze"
            | "erschrecke"
            | "lese"
            | "esse"
            | "verunstalte"
            | "programmiere"
            | "reibe"
            | "animiere"
            | "erschie\u00DFe"
            | "highlighte"
            | "beschimpfe"
            | "fotografiere"
            | "verkaufe"
            | "bastle"
            | "kategorisiere"
            )
            " "
            noun4
            [ " " adverb ]
            [ " " adverbial ]
    | <2>   ( "rede"
            | "schreibe"
            )
            [ " " adverb ]
            [ " " adverbial ]
            ( " \u00FCber " noun4
            | " von " noun3
            )
    ;

verb1_object_movement
    :   ( "schlage"
        )
        " "
        noun4
        [ " " adverb ]
        [ " " movement_adverbial ]
    ;

verb1_object_split
    :   ( "schaue"
        )
        " "
        noun4
        [ " " adverb ]
        [ " " adverbial ]
        " an"
    ;

verb1_object_complex
    :   "verwechsle"
        [ " " adverb ]
        [ " " adverbial ]
        " "
        noun4
        " mit "
        noun3
    |   "\u00E4u\u00DFere mich"
        [ " " adverb ]
        [ " " adverbial ]
        (   " zu "
            noun3
        |   " \u00FCber "
            noun4
        )
    |   "spiele"
        [ " " adverb ]
        [ " " adverbial ]
        " mit "
        noun3
    ;

verb3construct
    : !!opt_B   <3> verb3construct_builtin
    | !opt_g    <1> verb3construct_wg
    ;

verb3construct_builtin
    : <6> verb3_simple
    | <1> verb3_simple_reqadverb
    | <1> verb3_simple_movement
    | <11> verb3_self
    | <1> verb3_self_movement
    | <18> verb3_object_simple
    | <1> verb3_object_movement
    | <3> verb3_object_complex
    ;

verb3construct_wg
    :   [ adverb " " ]
        [ adverbial " " ]
        wg_verb3
    ;

verb3_simple
    :   [ adverb " " ]
        [ adverbial " " ]
        ( "redet"
        | "stinkt"
        | "spammt"
        | "sitzt"
        | "liegt"
        | "singt"
        )
    ;

verb3_simple_reqadverb
    :   adverb " "
        [ adverbial " " ]
        ( "klingt"
        )
    ;

verb3_simple_movement
    :   [ adverb " " ]
        [ movement_adverbial " " ]
        ( "wichst"
        )
    ;

verb3_self
    :   "sich "
        [ adverb " " ]
        [ adverbial " " ]
        ( "sieht"
        | "beschreibt"
        | "putzt"
        | "schl\u00E4gt"
        | "langweilt"
        | "erschreckt"
        | "animiert"
        | "erschie\u00DFt"
        | "beschimpft"
        | "fotografiert"
        | "verkauft"
        )
    ;

verb3_self_movement
    :   "sich "
        [ adverb " " ]
        [ movement_adverbial " " ]
        ( "entspannt"
        )
    ;

verb3_object_simple
    : <17>  [ adverb " " ]
            [ adverbial " " ]
            noun4
            " "
            ( "beschreibt"
            | "putzt"
            | "erschreckt"
            | "liest"
            | "isst"
            | "verunstaltet"
            | "programmiert"
            | "reibt"
            | "animiert"
            | "erschie\u00DFt"
            | "anschaut"
            | "highlightet"
            | "beschimpft"
            | "fotografiert"
            | "verkauft"
            | "bastelt"
            | "kategorisiert"
            )
    | <2>   [ adverb " " ]
            [ adverbial " " ]
            ( "von " noun3
            | "\u00FCber " noun4
            )
            " "
            ( "schreibt"
            | "redet"
            )
    ;

verb3_object_movement
    :   [ adverb " " ]
        noun4
        " "
        [ movement_adverbial " " ]
        ( "schl\u00E4gt"
        )
    ;

verb3_object_complex
    :   [ adverb " " ]
        [ adverbial " " ]
        noun4
        " mit "
        noun3
        ( " verwechselt"
        )
    |   "sich "
        [ adverb " " ]
        [ adverbial " " ]
        (   "zu "
            noun3
        |   "\u00FCber "
            noun4
        )
        " \u00E4u\u00DFert"
    |   [ adverb " " ]
        [ adverbial " " ]
        "mit "
        noun3
        " spielt"
    ;

// ARTICLES AND NOUNS

art_m1 : "der" | "ein" ;
art_m3 : "dem" | "einem" ;
art_m4 : "den" | "einen" ;

art_f1 : "die" | "eine" ;
art_f3 : "der" | "einer" ;
art_f4 : "die" | "eine" ;

art_n1 : "das" | "ein" ;
art_n3 : "dem" | "einem" ;
art_n4 : "das" | "ein" ;

noun1   : !!opt_B   noun{art_m1, art_f1, art_n1}
        | !opt_g    wg_noun{art_m1, art_f1, art_n1}
        ;
noun3   : !!opt_B   noun{art_m3, art_f3, art_n3}
        | !opt_g    wg_noun{art_m3, art_f3, art_n3}
        ;
noun4   : !!opt_B   noun{art_m4, art_f4, art_n4}
        | !opt_g    wg_noun{art_m4, art_f4, art_n4}
        ;

noun{art_m, art_f, art_n}
    : art_m " ROFL-Copter"
    | art_n " Katzenz\u00FCchtungsbuch"
    | art_f " Schultasche"
    | ("Slashdot" | "YouPorn" | "Wikipedia")
    | art_f " Zahnb\u00FCrste"
    | art_f " Klopapierrolle"
    | art_n " Pferd"
    | art_f " " ("Lehr" | "Auto" | "Flaschenpost") "-Industrie"
    | art_f " " ("Viagra" | "Cialis") "-Pille"
    | art_f " Cola-Dose"
    | art_f " Semmel"
    | art_n " Handy"
    | art_m " Saft"
    | art_f " Uhr"
    | art_n " Fenster"
    | art_f " " ("Kamera" | "Webcam")
    | (art_m | art_n) " Teller"
    | art_f " " ("Uni Wien" | "TU Wien")
    | art_n " Topic"
    | art_m " Clown"
    | art_m " Drucker"
    | art_m " Vogerlsalat"
    | art_f " Pizza-Porn-Seite"
    | art_m " " ("Installateuer" | "Monteur" | "Programmierer")
    | art_n " " ("Buch" | "Bilderbuch" | "Linux-Buch" | "GIMP-Buch" | "HMTL-Buch" | "CSS-Buch" | "Lotus-Notes-Buch") // HMTL [sic]
    | art_f " Steckdose"
    | "Substi"
    | art_m " Substi-Ersatz"
    | art_n " Deo"
    | (art_f | art_n) " Cola"
    | art_n " Papier"
    | art_f " Pr\u00E4sentation"
    | art_f " Katze"
    | "Schmierfett"
    | art_m " " ("Fleck" | "Ketchup-Fleck" | "Fett-Fleck")
    | art_f " Lesbe"
    | art_f " Teetasse"
    | art_m " Kugelschreiber"
    | art_m " Interpreter"
    | art_m " Channel"
    | art_m " Tisch"
    | art_m " Baum"
    | art_f " Gerichtsverhandlung"
    | art_m " Roboter"
    | art_f " Waschmaschine"
    // custom nouns:
    | art_m " Penis"
    | art_m " Handwerker"
    | art_m " Schre√üer"
    | "Ravus Bart"
    | person
    ;

// ADVERBS AND ADVERBIALS

adverb
    :   (   <14> ""
        |   <1> "vielleicht "
        |   <1> "etwas "
        |   <1> "eher "
        |   <1> "nicht "
        |   <1> "teilweise "
        |   <1> "sehr "
        |   <1> "ein wenig "
        )
        (   !!opt_B <3> adverb_builtin
        |   !opt_g  <1> wg_adverb
        )
        ;

adverb_builtin
    :
        ( "k\u00E4ltlich" | "gespannt" | "gl\u00FCcklich" | "verspannt" | "langsam"
        | "bl\u00F6d" | "suspekt" | "begeistert" | "schauend" | "schnell" | "redend"
        | "zufrieden" | "gelangweilt" | "entspannt" | "ruhig" | "erstaunt"
        | "wichsend" | "reibend" | "stinkend" | "froh" | "spammend"
        | noun4 " verkaufend"
        | "mit " noun3 " spielend"
        | "von " noun3 " begafft"
        | ("auf " | "neben ") noun3 " liegend"
        | noun4 " essend"
        )
    ;

adverbial
    : <5> ("\u00FCber" | "auf" | "bei" | "mit") " " noun3
    | <2> "zwischen " noun3 " und " noun3
    ;

movement_adverbial
    : adverbial
    | ("durch" | "um" | "in") " " noun4
    | ("aus" | "zu") " " noun3
    ;

opt_interjection
    : <3>  ""
    | <1>  ( "foobar" | "oida" | "heast" | "uppsala" | "lalala" | "ramba zamba"
            | "bla bla"
            )
            ", "
    ;

conjunction
    : "w\u00E4hrend" | "zudem" | "bis" | "wodurch" | "da" | "solange" | "womit"
    ;


// and now: word generation, stolen from a different viperBOT function

wg_verb1 : wg_prefix wg_root wg_verb1_suffix ;
wg_verb3
    : wg_prefix wg_root_t wg_verb3_suffix_t
    | wg_prefix wg_root_non_t wg_verb3_suffix_non_t
    ;
wg_adverb : wg_prefix wg_root wg_adverb_suffix ;
wg_noun{art_m, art_f, art_n}
    : art_m " " wg_cap_prefix wg_root
    | art_f " " wg_cap_prefix wg_root "e"
    | art_n " " wg_cap_prefix wg_root
    ;

wg_prefix
    : "an" | "be" | "um" | "auf" | "zu" | "ab" | "ge" | "miss" | "der" | "ver"
    ;

wg_cap_prefix
    : "An" | "Be" | "Um" | "Auf" | "Zu" | "Ab" | "Ge" | "Miss" | "Der" | "Ver"
    ;

wg_root : wg_root_t | wg_root_non_t ;

wg_root_t
    : <2> wg_preconsonants wg_vowel wg_postconsonants_t
    | <1> wg_vowel wg_postconsonants_t
    ;

wg_root_non_t
    : <2> wg_preconsonants wg_vowel wg_postconsonants_non_t
    | <1> wg_vowel wg_postconsonants_non_t
    ;

wg_preconsonants
    : <3> wg_consonant_simple
    | <1> wg_consonant_cluster
    ;

wg_consonant_simple
    : "b" | "c" | "d" | "f" | "g" | "h" | "k" | "l" | "m" | "n" "p" | "r" | "s"
    | "t" | "w" | "y" | "z"
    ;

wg_consonant_cluster
    : "str" | "fl" | "bl" | "kr" | "sch" | "tsch" | "br" | "gl" | "kl"
    ;

wg_vowel
    : <2> wg_vowel_simple
    | <1> wg_vowel_umlaut
    | <1> wg_vowel_diphthong
    ;

wg_vowel_simple : "a" | "e" | "i" | "o" | "u" ;
wg_vowel_umlaut : "\u00E4" | "\u00F6" | "\u00FC" ;
wg_vowel_diphthong : "ei" | "ai" | "ah" ;

wg_postconsonants_t
    : "chst" | "scht" | "cht" | "st"
    | "nt" | "st"
    ;

wg_postconsonants_non_t
    : "nk" | "\u00DF" | "ss" | "ff"
    ;

wg_verb1_suffix : "e" ;
wg_verb3_suffix_t : "et" ;
wg_verb3_suffix_non_t : "t" ;
wg_verbinf_suffix : "en" ;
wg_adverb_suffix
    : "bar" | "er" | "ig" | "isch" | "haft"
    ;
