// START SYMBOL

wtf3
    : "Ich " verb1construct ", " opt_interjection conjunction " " chosen_person " " verb3construct "."
    ;

// PERSONS

person : __IRC_nick ;
chosen_person : __IRC_chosen_nick;

// VERBS

verb1construct
    : <4> verb1_simple
    | <1> verb1_simple_movement
    | <13> verb1_self
    | <1> verb1_self_movement
    | <14> verb1_object_simple
    | <1> verb1_object_movement
    | <1> verb1_object_split
    | <1> verb1_object_complex
    ;

verb1_simple
    :   ( "rede"
        | "stinke"
        | "spamme"
        | "liege"
        )
        [ " " adverb ]
        [ " " adverbial ]
    ;

verb1_simple_movement
    :   ( "wichse"
        )
        [ " " adverb ]
        [ " " movement_adverbial ]
    ;

verb1_self
    :   ( "sehe"
        | "beschreibe"
        | "putze"
        | "schlage"
        | "langweile"
        | "aeussere"
        | "erschrecke"
        | "entspanne"
        | "animiere"
        | "erschiesse"
        | "beschimpfe"
        | "fotografiere"
        | "verkaufe"
        )
        " mich"
        [ " " adverb ]
        [ " " adverbial ]
    ;

verb1_self_movement
    :   ( "entspanne"
        )
        " mich"
        [ " " adverb ]
        [ " " movement_adverbial ]
    ;

verb1_object_simple
    :   ( "beschreibe"
        | "putze"
        | "erschrecke"
        | "lese"
        | "esse"
        | "verunstalte"
        | "programmiere"
        | "reibe"
        | "animiere"
        | "erschiesse"
        | "highlighte"
        | "beschimpfe"
        | "fotografiere"
        | "verkaufe"
        )
        " "
        noun4
        [ " " adverb ]
        [ " " adverbial ]
    |   ( "rede"
        )
        [ " " adverb ]
        [ " " adverbial ]
        " ueber "
        noun4
    ;

verb1_object_movement
    :   ( "schlage"
        )
        " "
        noun4
        [ " " adverb ]
        [ " " movement_adverbial ]
    ;

verb1_object_split
    :   ( "schaue"
        )
        " "
        noun4
        [ " " adverb ]
        [ " " adverbial ]
        " an"
    ;

verb1_object_complex
    :   "verwechsle"
        [ " " adverb ]
        [ " " adverbial ]
        " "
        noun4
        " mit "
        noun3
    ;

verb3construct
    : <4> verb3_simple
    | <1> verb3_simple_movement
    | <12> verb3_self
    | <1> verb3_self_movement
    | <15> verb3_object_simple
    | <1> verb3_object_movement
    | <1> verb3_object_complex
    ;

verb3_simple
    :   [ adverb " " ]
        [ adverbial " " ]
        ( "redet"
        | "stinkt"
        | "spammt"
        | "liegt"
        )
    ;

verb3_simple_movement
    :   [ adverb " " ]
        [ movement_adverbial " " ]
        ( "wichst"
        )
    ;

verb3_self
    :   "sich "
        [ adverb " " ]
        [ adverbial " " ]
        ( "sieht"
        | "beschreibt"
        | "putzt"
        | "schlaegt"
        | "langweilt"
        | "aeussert"
        | "erschreckt"
        | "animiert"
        | "erschiesst"
        | "beschimpft"
        | "fotografiert"
        | "verkauft"
        )
    ;

verb3_self_movement
    :   "sich "
        [ adverb " " ]
        [ movement_adverbial " " ]
        ( "entspannt"
        )
    ;

verb3_object_simple
    :   [ adverb " " ]
        [ adverbial " " ]
        noun4
        " "
        ( "beschreibt"
        | "putzt"
        | "erschreckt"
        | "liest"
        | "isst"
        | "verunstaltet"
        | "programmiert"
        | "reibt"
        | "animiert"
        | "erschiesst"
        | "anschaut"
        | "highlightet"
        | "beschimpft"
        | "fotografiert"
        | "verkauft"
        )
    ;

verb3_object_movement
    :   [ adverb " " ]
        noun4
        " "
        [ movement_adverbial " " ]
        ( "schlaegt"
        )
    ;

verb3_object_complex
    :   [ adverb " " ]
        [ adverbial " " ]
        noun4
        " mit "
        noun3
        ( " verwechselt"
        )
    ;

// ARTICLES AND NOUNS

art_m1 : "der" | "ein" ;
art_m3 : "dem" | "einem" ;
art_m4 : "den" | "einen" ;

art_f1 : "die" | "eine" ;
art_f3 : "der" | "einer" ;
art_f4 : "die" | "eine" ;

art_n1 : "das" | "ein" ;
art_n3 : "dem" | "einem" ;
art_n4 : "das" | "ein" ;

noun1 : noun{art_m1, art_f1, art_n1} ;
noun3 : noun{art_m3, art_f3, art_n3} ;
noun4 : noun{art_m4, art_f4, art_n4} ;

noun{art_m, art_f, art_n}
    : art_m " ROFL-Copter"
    | art_n " Katzenzuechtungsbuch"
    | art_f " Schultasche"
    | ("Slashdot" | "YouPorn")
    | art_f " Zahnbuerste"
    | art_f " Klopapierrolle"
    | art_n " Pferd"
    | art_f " " ("Lehr" | "Auto" | "Flaschenpost") "-Industrie"
    | art_f " " ("Viagra" | "Cialis") "-Pille"
    | art_f " Cola-Dose"
    | art_f " Semmel"
    | art_n " Handy"
    | art_m " Saft"
    | art_f " Uhr"
    | art_n " Fenster"
    | art_f " " ("Kamera" | "Webcam")
    | (art_m | art_n) " Teller"
    | art_f " " ("Uni Wien" | "TU Wien")
    | art_n " Topic"
    | art_m " Clown"
    | art_m " Drucker"
    | art_m " Vogerlsalat"
    | art_f " Pizza-Porn-Seite"
    | art_m " " ("Handwerker" | "Installateuer" | "Programmierer")
    | art_n " " ("Buch" | "Bilderbuch" | "Linux-Buch")
    | art_f " Steckdose"
    | "Substi"
    | art_m " Substi-Ersatz"
    | art_n " Deo"
    | (art_f | art_n) " Cola"
    | art_n " Papier"
    | art_f " Praesentation"
    | art_f " Katze"
    | "Schmierfett"
    | art_m " Fleck"
    | art_f " Lesbe"
    | art_f " Teetasse"
    | art_m " Kugelschreiber"
    | art_m " Interpreter"
    // custom nouns:
    | art_m " Penis"
    | "Ravus Bart"
    | person
    ;

// ADVERBS AND ADVERBIALS

adverb
    :   (   <6> ""
        |   <1> "vielleicht "
        |   <1> "etwas "
        )
        ( "kaeltlich" | "spielend" | "gespannt" | "gluecklich" | "verspannt"
        | "bloed" | "suspekt" | "begeistert" | "schauend" | "schnell" | "redend"
        | "zufrieden" | "gelangweilt" | "entspannt" | "begafft" | "ruhig"
        | "verkaufend"
        )
    ;

adverbial
    : <50> ("ueber" | "auf" | "mit") " " noun3
    | <20> "zwischen " noun3 " und " noun3
    ;

movement_adverbial
    : adverbial
    | ("durch" | "um" | "in") " " noun4
    | ("aus" | "zu") " " noun3
    ;

opt_interjection
    : <75>  ""
    | <25>  ( "foobar" | "oida" | "heast" | "uppsala" | "lalala" | "ramba zamba"
            )
            ", "
    ;

conjunction
    : "waehrend" | "zudem" | "bis" | "wodurch" | "da" | "solange" | "womit"
    ;
